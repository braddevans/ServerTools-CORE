buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.12.1'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = '3.0.0'
minecraft.version = '1.8.8-11.15.0.1608-1.8.8'
minecraft.mappings = 'snapshot_20151202'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    // TODO: Forge ships an older version of hocon-config. Need to figure out how to handle this
    compile(group: 'ninja.leaping.configurate', name: 'configurate-hocon', version: '3.0') {
        exclude module: 'guava'
    }

    testCompile group: 'junit', name: 'junit', version: '4.12'
}

version = "$minecraft.version-$version"
if (System.getenv().'BUILD_NUMBER' != null) {
    version += ".${System.getenv().'BUILD_NUMBER'}"
} else {
    version += ".0"
}

minecraft {
    runDir = 'run'
    makeObfSourceJar = false
}

[jar, sourceJar, shadowJar]*.exclude 'log4j2-test.xml'

jar {
    classifier = 'nodeps'
}

shadowJar {
    classifier = '' // This is now the main jar
    relocate 'ninja.leaping.configurate', 'info.servertools.repack.ninja.leaping.configurate'
    relocate 'com.typesafe.config', 'info.servertools.repack.com.typesafe.config'
}

reobf {
    shadowJar { useSrgSrg() }
}

tasks.build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

processResources {

    from 'LICENSE'

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand([
                'version'     : project.version,
                'mcversion'   : project.minecraft.version,
                'url'         : project.url,
                'description' : project.description,
                'forgeversion': project.minecraft.forgeVersion
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.year = project.inceptionYear
    ext.url = project.url
    exclude '**/*.info'
    exclude '**/*.properties'
    exclude '**/*.cfg'
    header = file('HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures = false
    strictCheck = true
    mapping { java = 'SLASHSTAR_STYLE' }
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}