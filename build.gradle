buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
//    id 'net.minecraftforge.gradle.forge' version '2.0.2'
    id 'com.github.hierynomus.license' version '0.12.1'
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = '3.0.0'
minecraft.version = '1.8.8-11.14.4.1576-1.8.8'
minecraft.mappings = 'snapshot_20151122'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile(group: 'ninja.leaping.configurate', name: 'configurate-hocon', version: '2.0') {
        exclude module: 'guava'
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

version = "$minecraft.version-$version"
if (System.getenv().'BUILD_NUMBER' != null) {
    version += ".${System.getenv().'BUILD_NUMBER'}"
} else {
    version += ".0"
}

minecraft {
    runDir = 'run'
    makeObfSourceJar = false
}

[jar, sourceJar]*.exclude 'log4j2-test.xml'

processResources {

    from 'LICENSE'

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        expand([
                'version'     : project.version,
                'mcversion'   : project.minecraft.version,
                'url'         : project.url,
                'description' : project.description,
                'forgeversion': project.minecraft.forgeVersion
        ])
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

license {
    ext.name = project.name
    ext.organization = project.organization
    ext.year = project.inceptionYear
    ext.url = project.url
    exclude '**/*.info'
    exclude '**/*.properties'
    exclude '**/*.cfg'
    header = file('HEADER.txt')
    sourceSets = project.sourceSets
    ignoreFailures = false
    strictCheck = true
    mapping { java = 'SLASHSTAR_STYLE' }
}

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
    task.options.fork = true
}

jar {
    configurations.shade.each { dep ->
        from(project.zipTree(dep)) {
            exclude 'META-INF', 'META-INF/**'
        }
    }
}
